#!/bin/zsh
# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

export MANWIDTH=80
export MANPAGER='nvim +Man!'

# man zshbuiltins
# -U options, for array (but not for associative arrays) keep only the first occurrence of each duplicated value.
typeset -U PATH path

eval "$(~/.local/bin/mise activate zsh)"

typeset -U config_files
typeset -U fns
config_files=($HOME/.config/zsh/confs/*.zsh)
fns=($HOME/.config/zsh/confs/functions/*.zsh)

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# Move with Vi like keys through the
# menu of generated completions.
# NOTE:
# This needs to be defined before initializing 
# the completion system (compinit)
zmodload zsh/complist
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history

autoload -Uz compinit 
if [[ -n ${HOME}/.zcompdump(#qN.mh+24) ]]; then
	compinit;
else
	compinit -C;
fi;

# load zstyle rules after compinit initialization
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done
#
## load functions inside functions directory
for file in ${fns}; do
  source "$file"
done

unset config_files
unset fns

# Init zoxide
eval "$(zoxide init zsh)"
unalias z 2> /dev/null

#source /home/sigfried/.profile
source /usr/share/zsh-theme-powerlevel10k/powerlevel10k.zsh-theme

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# load antidote plugins
source /usr/share/zsh-antidote/antidote.zsh
antidote load

# To customize prompt, run `p10k configure` or edit ~/.config/zsh/.p10k.zsh.
[[ ! -f ~/.config/zsh/.p10k.zsh ]] || source ~/.config/zsh/.p10k.zsh
